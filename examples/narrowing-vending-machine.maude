---- from the book All About Maude
---- from the Maude Manual
---- by the Maude team

mod NARROWING-VENDING-MACHINE is
  sorts Coin Item Marking Money State .
  subsort Coin < Money .
  op empty : -> Money .
  op __ : Money Money -> Money [assoc comm id: empty] .
  subsort Money Item < Marking .
  op __ : Marking Marking -> Marking [assoc comm id: empty] .
  op <_> : Marking -> State .
  ops $ q : -> Coin .
  ops a c : -> Item .
  var M : Marking .
  rl [buy-c] : < M $ > => < M c > [narrowing] .
  rl [buy-a] : < M $ > => < M a q > [narrowing] .
  eq [change] : q q q q M = $ M [variant] .
endm

eof

vu-narrow [1] in NARROWING-VENDING-MACHINE : < M:Money > =>* < a c > .
vu-narrow [,5] in NARROWING-VENDING-MACHINE : < M:Money > =>* < a c > .
vu-narrow [,5] in NARROWING-VENDING-MACHINE : < M:Money > =>! < a c > .
vu-narrow [,10] in NARROWING-VENDING-MACHINE :
           < C1:Coin C2:Coin C3:Coin C4:Coin > =>! < M:Money a c > .
vu-narrow [, 2] in NARROWING-VENDING-MACHINE :
         < $ q q q M1:Money > =>* < a c M2:Money > .
vu-narrow {filter} [, 2] in NARROWING-VENDING-MACHINE :
         < $ q q q M1:Money > =>* < a c M2:Money > .
vu-narrow {filter,delay} [, 2] in NARROWING-VENDING-MACHINE :
         < $ q q q M1:Money > =>* < a c M2:Money > .
fvu-narrow [1] in NARROWING-VENDING-MACHINE : < M:Money > =>* < a c > .
fvu-narrow in NARROWING-VENDING-MACHINE : < M:Marking > =>* < a c > .
