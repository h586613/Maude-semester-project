---- from the book All About Maude
---- from the Maude Manual
---- by the Maude team

load socket.maude

mod FACTORIAL-SERVER is
  inc SOCKET .
  pr CONVERSION .

  op _! : Nat -> NzNat .
  eq 0 ! = 1 .
  eq (s N) ! = (s N) * (N !) .

  op Server : -> Cid .
  op aServer : -> Oid .

  vars O LISTENER CLIENT : Oid .
  var  A : AttributeSet .
  var  N : Nat .
  vars IP DATA S : String .

  rl [createdSocket] :
    < O : Server | A > createdSocket(O, socketManager, LISTENER)
    => < O : Server | A > acceptClient(LISTENER, O) .

  rl [acceptedClient] :
    < O : Server | A > acceptedClient(O, LISTENER, IP, CLIENT)
    => < O : Server | A > receive(CLIENT, O)
       acceptClient(LISTENER, O) .

  rl [received] :
    < O : Server | A > received(O, CLIENT, DATA)
    => < O : Server | A >
       send(CLIENT, O, string(rat(DATA, 10)!, 10)) .

  rl [sent] :
    < O : Server | A > sent(O, CLIENT)
    => < O : Server | A > closeSocket(CLIENT, O) .

  rl [closedSocket] :
    < O : Server | A > closedSocket(O, CLIENT, S)
    => < O : Server | A > .
endm

***(
Maude> erew <>
            < aServer : Server | none >
            createServerTcpSocket(socketManager, aServer, 8811, 5) .
erewrite in FACTORIAL-SERVER : <> < aServer : Server | none >
    createServerTcpSocket(socketManager, aServer, 8811, 5) .
)
